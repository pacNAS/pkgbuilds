# $Id: PKGBUILD 189328 2013-07-03 01:24:05Z eric $
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

pkgname=elfutils
pkgver=0.155
pkgrel=1
pkgdesc="Libraries and utilities to handle ELF object files and DWARF debugging information"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('gcc-libs' 'zlib' 'bzip2' 'xz')
provides=('libelf')
replaces=('libelf')
conflicts=('libelf')
options=('!makeflags')
source=(https://fedorahosted.org/releases/e/l/elfutils/${pkgver}/elfutils-${pkgver}.tar.bz2{,.sig}
        elfutils-0.155-binutils-pr-ld-13621.patch)
sha1sums=('1b96084cd9fe037c1c0bd2c280aaa701f913462b'
          'fcd0ebd029b305bf030983daf151a2a1ab5294af'
          '8a8be7c0686c6311c1ce969ba10494e704ead48c')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../elfutils-0.155-binutils-pr-ld-13621.patch
  sed -i -e 's:-Werror::g' $(find -name Makefile.in)
}

build() {
  cd ${pkgname}-${pkgver}
  CFLAGS+=" -g"  # required for test-suite success
  ./configure --prefix=/usr --program-prefix="eu-"
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
