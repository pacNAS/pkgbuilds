# $Id: PKGBUILD 191941 2013-08-01 13:10:14Z dreisner $
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor (for pacnas): Markus M. May <triplem@pacnas.org>

pkgname=mkinitcpio
pkgver=0.15.0
pkgrel=100
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url="http://www.archlinux.org/"
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive'
         'coreutils' 'bash' 'findutils' 'grep' 'filesystem>=2011.10-1' 'gzip' 'systemd')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
backup=('etc/mkinitcpio.conf')
source=("ftp://ftp.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz")
install=mkinitcpio.install
sha256sums=('2a00421c5ce4abfcc971c71a6efd22f6bbaf0e541f9a93410f35e864c818c3db')

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  # remove fsck hook, cause we are using btrfs, and this leads to errors
  # during the creation of the image (see https://bugs.archlinux.org/task/37203)
  # enable btrfs hook ;-)
  sed -e "s|HOOKS=\"base udev autodetect modconf block filesystems keyboard fsck\"|HOOKS=\"base udev autodetect modconf block filesystems keyboard btrfs_advanced\"|" \
      -i $pkgdir/etc/mkinitcpio.conf
}

