# Maintainer: Valentin Hăloiu <vially.ichb+aur [at] gmail.com>
# Contributor: Étienne Deparis <etienne [at] depar.is>
# Contributor: Yannik Stein <yannik.stein [at] gmail.com>
# Submitter: vbPadre <vbpadre [at] gmail.com>
# Contributor: Markus M. May <triplem@pacnas.org>

pkgname=ajenti
pkgver=1.1.8
pkgrel=1
pkgdesc="An easy server administration frontend."
arch=('any')
url="http://ajenti.org/"
license=('LGPL')
depends=('python2' 'python2-gevent' 'python2-gevent-socketio'
  'python2-lxml' 'python2-passlib' 'python2-psutil' 'python2-daemon'
  'python2-catcher' 'python2-reconfigure' 'python2-pyopenssl'
  'python2-pillow' 'python2-exconsole' 'stunnel' 'coffee-script' 'lessc'
  'yuicompressor' 'python2-ldap' )
makedepends=('python2' 'coffee-script' 'lessc' 'yuicompressor' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Eugeny/$pkgname/archive/$pkgver.tar.gz"
  "ajenti.install"
  "ajenti.service")
install=ajenti.install
md5sums=('f50e7eebe0a63ac0ad42b99066376337' '6c0c71089a9c59e07c62e87baff20659' 'ebe892b044f0163e8d092bd6568b6c2b')
backup=('etc/ajenti/config.json')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Replacing python shebang by python2 shebang...'
  find . -type f -exec sed -i \
    -e'1s|^#!/usr/bin/env python$|#!/usr/bin/env python2|' \
    -e '1s|^#!/usr/bin/python$|#!/usr/bin/env python2|' \
    "{}" \;
  sed -i -e '1s/`which python`/python2/' Makefile

  msg2 'Replacing yui-compressor by yuicompressor...'
  find . -type f -exec sed -i -e's/yui-compressor/yuicompressor/' "{}" \;
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Build tgz archive'
  make tgz

  cd "$srcdir/$pkgname-$pkgver/dist"
  tar xzf $pkgname-$pkgver.tar.gz
}

package() {
  cd "$srcdir/$pkgname-$pkgver/dist/$pkgname-$pkgver"

  python2 setup.py install --root=$pkgdir --optimize=1

  # Fix config file permissions
  chmod o-rwx "$pkgdir/etc/ajenti/config.json"

  rm -r $pkgdir/etc/init.d

  install -D $srcdir/ajenti.service $pkgdir/usr/lib/systemd/system/ajenti.service
}
